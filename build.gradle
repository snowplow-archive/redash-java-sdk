// --- Configure Plugins and Repositories

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}

plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.6.3'
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'com.jfrog.bintray'

wrapper.gradleVersion = '2.14'

group 'com.snowplowanalytics.redash'
version '0.1.0'

sourceCompatibility = 1.8

jacoco {
    toolVersion = "0.7.7.201606060606"
    reportsDir = file("$buildDir/reports/")
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.9.1'
    compile group: 'org.json', name: 'json', version: '20171018'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'

    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// --- Configure Tasks

task redashSetup(type: Exec) {
    commandLine 'sh', './integration/setup_redash.bash'
}

task redashDestroy(type: Exec) {
    commandLine 'sh', './integration/remove_redash.bash'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        xml.destination = "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        html.enabled = true
        html.destination = "${buildDir}/reports/jacoco/test/html"
    }
}

test {
    dependsOn redashSetup
    finalizedBy jacocoTestReport
}

// --- Deployment

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task cleanDocs(type: Exec) {
    commandLine "rm", "-rf", "docs/"
}

task commitDocs(type: Exec) {
    dependsOn clean
    dependsOn javadoc
    dependsOn cleanDocs
    commandLine "cp", "-a", "${buildDir}/docs/javadoc/.", "docs/"
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_SNOWPLOW_MAVEN_USER')
    key = System.getenv('BINTRAY_SNOWPLOW_MAVEN_API_KEY')

    publish = true
    override = false

    publications = ['mavenJava']

    pkg {
        repo = 'snowplow-maven'
        name = 'redash-java-sdk'
        group = "${project.group}"
        userOrg = 'snowplow'

        websiteUrl = 'https://github.com/snowplow-incubator/redash-java-sdk'
        issueTrackerUrl = 'https://github.com/snowplow-incubator/redash-java-sdk/issues'
        vcsUrl = 'https://github.com/snowplow-incubator/redash-java-sdk'

        version.name = "${project.version}"
    }
}
